apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

apply plugin: 'deploygate'

repositories {
    maven {
        url "https://c-lis.bintray.com/maven"
    }
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    signingConfigs {
        def propFile = project.rootProject.file('release.properties')
        if (!propFile.exists()) {
            propFile.createNewFile()
        }

        def properties = new Properties()
        properties.load(propFile.newDataInputStream())

        def signStoreFilePath = properties.getProperty("signing.storeFilePath") ?: ""
        def signStorePassword = properties.getProperty("signing.storePassword") ?: ""
        def signKeyAlias = properties.getProperty("signing.keyAlias") ?: ""
        def signKeyPassword = properties.getProperty("signing.keyPassword") ?: ""

        release {
            storeFile file(signStoreFilePath)
            storePassword signStorePassword
            keyAlias signKeyAlias
            keyPassword signKeyPassword
        }
    }

    defaultConfig {
        applicationId "jp.co.c_lis.mangaview.android"
        minSdkVersion 15
        targetSdkVersion 29
        versionCode version_code
        versionName version_name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    implementation project(path: ':mangaview')
    // implementation "dev.keiji.mangaview:mangaview:$version_code"
}

deploygate {
    def propFile = project.rootProject.file('release.properties')
    if (!propFile.exists()) {
        propFile.createNewFile()
    }

    def properties = new Properties()
    properties.load(propFile.newDataInputStream())

    appOwnerName = properties.getProperty("deploygate.app_owner_name") ?: ""
    apiToken = properties.getProperty("deploygate.api_token") ?: ""

    deployments {
        release {
            def hash = "git rev-parse --short HEAD".execute([], project.rootDir).in.text.trim()
            message = "Sample build ${hash}"
        }
    }
}
