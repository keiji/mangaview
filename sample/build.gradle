plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'deploygate'
}

android {
    compileSdkVersion compile_sdk_version
    buildToolsVersion build_tools_version

    signingConfigs {
        def propFile = project.rootProject.file('release.properties')
        if (!propFile.exists()) {
            propFile.createNewFile()
        }

        def properties = new Properties()
        properties.load(propFile.newDataInputStream())

        def signStoreFilePath = properties.getProperty("signing.storeFilePath")
        def signStorePassword = properties.getProperty("signing.storePassword") ?: ""
        def signKeyAlias = properties.getProperty("signing.keyAlias") ?: ""
        def signKeyPassword = properties.getProperty("signing.keyPassword") ?: ""

        release {
            if (signStoreFilePath == null) {
                return
            }

            storeFile file(signStoreFilePath)
            storePassword signStorePassword
            keyAlias signKeyAlias
            keyPassword signKeyPassword
        }
    }

    defaultConfig {
        applicationId "jp.co.c_lis.mangaview.android"
        minSdkVersion min_sdk_version
        targetSdkVersion target_sdk_version
        versionCode version_code
        versionName version_name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    def coroutines_version = "1.5.0"

    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.core:core-ktx:1.6.0'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.1'
    implementation 'androidx.viewpager2:viewpager2:1.1.0-beta01'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    implementation project(path: ':mangaview')
//    implementation "dev.keiji.mangaview:mangaview:$version_name"
}

deploygate {
    def propFile = project.rootProject.file('release.properties')
    if (!propFile.exists()) {
        propFile.createNewFile()
    }

    def properties = new Properties()
    properties.load(propFile.newDataInputStream())

    appOwnerName = properties.getProperty("deploygate.app_owner_name") ?: ""
    apiToken = properties.getProperty("deploygate.api_token") ?: ""

    deployments {
        release {
            def hash = "git rev-parse --short HEAD".execute([], project.rootDir).in.text.trim()
            message = "Sample build ${hash}"
        }
    }
}
